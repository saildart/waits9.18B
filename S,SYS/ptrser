COMMENT ⊗   VALID 00005 PAGES
C REC  PAGE   DESCRIPTION
C00001 00001
C00002 00002	POWER BINARY BUSY DONE NULL RUBOUT PTRPOW PTRNPW STOPTR
C00004 00003	PTRDSP PTRINI PTRXRL
C00006 00004	PTRINP PTRU1 PTRU3 PTRHNG PTRXIT XPOPJ PTRP1H PTRP1 PTRP1A
C00012 00005		BEND PTRSER
C00013 ENDMK
C⊗;
;POWER BINARY BUSY DONE NULL RUBOUT PTRPOW PTRNPW STOPTR

IFN FTP2,<	;Whole file

BEGIN PTRSER ↔ SUBTTL PTRSER	PAPER TAPE READER SERVICE; INITIALIZE, RELEASE
;REG 9-28-75; JS 11-11-68

HISYS

;PTR CONTROL REGISTER
POWER←←400		;POWER ON ON PDP-6, NOT EOT ON PDP-10.
BINARY←←40		;BINARY MODE.
BUSY←←20		;PTR IS BUSY READING A WORD.
DONE←←10		;PTR HAS A WORD.

;SPECIAL ASCII CHARACTERS:
NULL←←200		;NULL CHARACTER
RUBOUT←←377		;RUBOUT.

;SPECIAL BITS IN LH OF IOS:
PTRPOW←←200000		;PTR HAS (HAD) POWER ON.
PTRNPW←←100000		;PDP-6 PTR POWER IS OFF WHEN ATTEMPTING FIRST OPERATION.
STOPTR←←40000		;HUNG TIMEOUT ASSUMES EOF.
;PTRDSP PTRINI PTRXRL

;UUO DISPATCH TABLE:
	JRST PTRINI		;INITIALIZE.
	JRST PTRHNG		;HUNG  TIMEOUT.
↑PTRDSP:JRST PTRXRL		;RELEASE.
	POPJ P,			;CLOSE (OUTPUT)
	JRST ILLOUT		;OUTPUT
	JRST PTRINP		;INPUT

;HERE FROM SYSINI
PTRINI:	SETZM PTRADV		;DON'T ALLOW GARBAGE IN COMM AREA TO FIRE PTRP1
	SYNINI PTRCSC		;INITIALIZE SYNCHRONIZER CELLS.
	SETZM PTRIOS		;CLEAR READER STATUS.  RELEASE UUO.
	POPJ P,

PTRXRL:	PSYNC PTRCSC		;SYNCHRONIZE.
	MOVE IOS,PTRIOS		;GET IOS
	TLO IOS,STOPTR!DEVSBB	;MAKE PTR STOP.
	SETOM PTRACT		;KICK P2
	PUSHJ P,PTRXIT		;STORE IOS, DESYNCHRONIZE.
	JRST WSYNC		;WAIT UNTIL P2 SIGNALS READER IS STOPPED.
;PTRINP PTRU1 PTRU3 PTRHNG PTRXIT XPOPJ PTRP1H PTRP1 PTRP1A

;INPUT UUO, HUNG TIME OUT, P1 CLOCK LEVEL - PTRINP, PTRP1

PTRINP:	PSYNC PTRCSC		;INPUT UUO
	MOVE IOS,PTRIOS		;PICKUP IOS AFTER SYNCHRONIZING
	TLNN IOS,IOBEG		;FIRST TIME?
	JRST PTRU3		;NO. - SEE IF THERE'S A BUFFER READY YET.
	TLO IOS,IOFST		;YES.  SET UP THINGS.
	TLZ IOS,PTRPOW		;HAVEN'T SEEN POWER ON (TAPE) YET.
	MOVE TAC,[41,,PTRBF2+1-P2VORG]
	MOVEM TAC,PTRBF1+1
	MOVE TAC,[41,,PTRBF1+1-P2VORG]
	MOVEM TAC,PTRBF2+1
	HRRZM TAC,PTRBDP	;POINTER TO DEPOSIT BUFFER
	HRRZM TAC,PTRBTP	;POINTER TO TAKER BUFFER.
	MOVSI TAC,4400		;ASSUME 36-BIT BYTES - I, IB, OR B
	TRNN IOS,10		;IS MODE LESS THAN 10?
	MOVSI TAC,700		;YES, SET 7 BIT BYTES - A OR AL MODE
	MOVEM TAC,PTRPTR	;SAVE FOR INTERRUPT ROUTINES.
PTRU1:	SETOM PTRACT		;REQUEST P2 TO START READER
	TLO IOS,DEVSBB
	PUSHJ P,STOIOS
	XSYNC PTRCSC
	PUSHJ P,WSYNC		;WAIT FOR SOME BUFFER TO FILL.
	PSYNC PTRCSC		;GAIN CONTROL OF PTRIOS.
	MOVE IOS,PTRIOS

PTRU3:	MOVE TAC1,PTRBTP	;GET POINTER TO TAKE BUFFER
	MOVEI TAC,(TAC1)
	PUSHJ P,P2ADCK		;CHECK FOR VALID ADDRESS
	 JRST DEVHNG		;SOMEONE STEPPED ON P2 CORE
	ADDI TAC1,P2VORG
	SKIPL TAC,(TAC1)	;SKIP IF TAKE BUFFER IS FULL.
	JRST PTRU1		;BUFFER NOT READY.  ASK READER FOR MORE.
	XSYNC PTRCSC
	HRRZM TAC,PTRBTP	;(ADVANCE THE TAKER)
	HRRZ TAC,DEVIAD(DDB)	;ADDRESS OF USER BUFFER TO FILL.
	JUMPE TAC,CPOPJ		;ARGLEBARGLE! WHERE'S HIS BUFFER?
	MOVSI AC1,1(TAC1)	;SOURCE OF BLT.
	HRRI AC1,1(TAC)		;DESTINATION (WC,CHECKSUM WORD IN BUFFER)
	XCTR XLB,[LDB AC2,[POINT 17,(TAC),17]]	;SIZE OF U BUFFER.
	CAIGE AC2,41		;MUST BE AT LEAST AS BIG AS SYSTEM BUFFER
	JRST BTLERR		;SYSTEM BUFFER TOO LARGE FOR USER BUFFER.
	ADDI AC2,(TAC)		;COMPUTE ENDING ADDRESS IN USER CORE.
	XCTR XBLTW,[BLT AC1,(AC2)]	;STORE DATA. SYSTEM TO USER.
;PARTS OF THE FOLLOWING ADAPTED FROM ADVBFF
	MOVE AC1,-1(TAC1)	;GET IOS ASSOCIATED W/SYSTEM BUFFER.
	MOVSI TAC,IOUSE
	ANDCAM TAC,(TAC1)	;MARK SYSTEM BUFFER AS FREE
	HRRZ TAC1,DEVIAD(DDB)	;GET BUFFER ADDRESS
	XCTR XW,[MOVEM AC1,-1(TAC1)]	;STORE IOS ASSOCIATED WITH THIS BUFFER.
	XCTR XRW,[IORB TAC,(TAC1)]	;MARK USER BUFFER AS FULL
	XCTR XRW,[MOVES (TAC)]	;ADDRESS CHECK NEXT BUFFER.
	HRRM TAC,DEVIAD(DDB)	;OK. STORE IT IN THE DDB
	MOVEI TAC1,(TAC)	;COPY BUFFER ADDRESS.
	XCTR XR,[SKIPGE TAC,(TAC)]	;IS THE NEXT U BUFFER FREE?
	POPJ P,			;NO. STILL BUSY
	HLRZ TAC,TAC		;TAC←BUFFER SIZE
	ADDI TAC1,(TAC)		;TAC1←ADDRESS OF END OF THE BUFFER
	XCTR XRW,[MOVES (TAC1)]	;ADDRESS CHECK THE END OF THE BUFFER
	MOVE TAC,PTRBTP
	PUSHJ P,P2ADCK		;CHECK FOR VALID ADDRESS
	 JRST DEVHNG		;SOMEONE STEPPED ON P2 CORE
	SKIPGE P2VORG(TAC)
	TRNE IOS,IOCON+740000	;SKIP IF ANY ERRORS OR IOCON MODE.
	POPJ P,			;RETURN FULL BUFFER TO USER. EXIT UUOCON
	JRST PTRINP		;MOVE NEXT BUFFER FROM S TO U.

;P1 CLOCK LEVEL AND HUNG TIMEOUT CODE

PTRHNG:	PSYNC PTRCSC		;SYNCHRONIZE.
	MOVE IOS,PTRIOS		;GET IOS
	TLZN IOS,PTRPOW		;HAVE WE SEEN POWER ON?
	JRST XPOPJ		;NO. LET IT HANG. (P2 IS DEAD?)
	TLON IOS,STOPTR		;MAKE PTR STOP.
	AOS (P)			;SET SKIP RETURN TO AVOID HUNG PTR MESSAGE
	SETOM PTRACT		;KICK P2
PTRXIT:	PUSHJ P,STOIOS		;STORE IOS, RESET HUNG COUNT
XPOPJ:	XSYNC PTRCSC
	POPJ P,			;DISMISS INTERRUPT.

;P1 HERE AT CLOCK LEVEL TO RESET HUNG TIME COUNT
↑PTRP1H:MOVEI DDB,PTRDDB
	SETZM PTRDAT+PTPSHT	;CLEAR FLAG THAT GOT US HERE
	LDB TAC,PDVTIM
	DPB TAC,PDVCNT
	POPJ P,

;HERE AT P1 CLOCK LEVEL IF PTRADV IS SET
↑PTRP1:	SETZM PTRADV		;CLEAR REQUEST
	PSYNC PTRCSC
	MOVEI DDB,PTRDDB	;LOAD UP REGISTERS.
	MOVE IOS,PTRIOS
	PUSHJ P,PTRP1A
	JRST PTRXIT		;STORE IOS, RELEASE SYNC. POPJ.

PTRP1A:	TLZE IOS,IOW		;IS JOB WAITING FOR PTR?
	PUSHJ P,SETIOD		;YES.  UNWAIT IT.
	TLZ IOS,DEVSBB
	POPJ P,
	BEND PTRSER

>;IFN FTP2	;Whole file
