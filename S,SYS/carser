COMMENT ⊗   VALID 00002 PAGES
C REC  PAGE   DESCRIPTION
C00001 00001
C00002 00002	CARDSP CARINI CARREL CARCLS CAROUT CAROUL CAROUN CAROU0 CARCLK CARNXT CARNXX CARNXY CARNXZ
C00008 ENDMK
C⊗;
;CARDSP CARINI CARREL CARCLS CAROUT CAROUL CAROUN CAROU0 CARCLK CARNXT CARNXX CARNXY CARNXZ

BEGIN CARSER ↔ SUBTTL CART SERVICE ROUTINE. HPM/REG 5-16-75

IFN CARNUM,<
;THIS ASSUMES THAT THE BUFFERS ARE IN LOWER SEGMENT!


; CART SERVICE DISPATCH TABLE

	JRST	CARINI		;INITILIZE
	JRST	CARREL		;not used, would be HUNG TIMEOUT.
↑CARDSP:JRST	CARREL		;RELEASE
	JRST	CARCLS		;CLOSE
	JRST	CAROUT		;OUTPUT
	JRST	ILLINP		;INPUT

CARINI:
CARREL:	MOVEI	IOS,0		;CLEAR IOS
	HRLOI	TAC,377777	;INHIBIT CART CODE IN CLKSER
	MOVEM	TAC,CARALR
	JRST	STOIOS		;STORE IOS, RETURN


CARCLS: PUSHJ	P,OUT		;FORCE ALL THE BUFFERS OUT
	JRST	WAIT1		;WAIT FOR SYSTEM BUFFER FREE

CAROUT:	PUSHJ	P,WAIT1		;MAKE SURE SYSTEM BUFFER IS FREE, WAIT IF NEEDED
	PUSHJ	P,DEVSTU	;SET UP TO PROCESS THIS BUFFER.
	POPJ	P,		;BUFFER IS ALREADY EMPTY.
	JUMPE	AC2,CAROU0	;JUMP IF THERE'S NOTHING IN IT
	CAILE	AC2,=20		;SMALLER THAN SIZE OF CARSER INTERNAL BUFFER?
	MOVEI	AC2,=20		;NO.  SET TO MAXIMUM SIZE
	MOVNI	AC2,(AC2)
	HRL	AC3,AC2		;MAKES -WC,,USERBUFFER IN AC3
	MOVEI	AC2,CARBUF-1	;MAKES 0,,CARBUF-1 IN AC2, FOR PUSH
CAROUL:	XCTR	XR,[MOVE TAC,(AC3)]	;FETCH A WORD FROM THE USER'S BUFFER
	ANDCMI	TAC,777		;CLEAR 9 LOW ORDER BITS
	JUMPG	TAC,CAROUN	;TEST TO SEE IF IT'S A CONTROL WORD
	TLZ	TAC,777000	;IF SO, CLEAR THE "CONTROL WORD" FLAG
	MOVEM	TAC,CARDEF	;AND SET THE CART DEFAULT WORD
	JRST	.+2
CAROUN:	PUSH	AC2,TAC		;IF NOT CONTROL, MOVE IT TO THE SYSTEM BUFFER
	AOBJN	AC3,CAROUL	;AND GO ON TO THE NEXT WORD, IF ANY
	HRRI	AC2,0		;WC,,0
	MOVN	AC2,AC2		;-WC IN AC2 LEFT
	JUMPE	AC2,.+2		;IF WC = 0 THERE'S NO DATA IN CARBUF (BUT IN CARDEF)
	HRRI	AC2,CARBUF	;-WC,,CARBUF = AOBJN POINTER.
	MOVEM	AC2,CARBPT	;
	TLO	IOS,DEVSBB!IO	;SET BUFFER BUSY.
	SETZM	CARALR		;AND CRANK UP THE CODE IN CLKSER
CAROU0:	PUSHJ	P,ADVBFE	;ADVANCE TO NEXT BUFFER
	JFCL
	JRST	STOIOS		;STORE DEVSBB IF NEEDED.

↑CARCLK:MOVEI	DDB,CARDDB	;LET WHOM IT CONCERNS KNOW CAR IS THE DEVICE
	MOVE	IOS,DEVIOS(DDB)
	TLZ	IOS,DEVSBB	;SYSTEM BUFFER NO LONGER BUSY.
	TLZE	IOS,IOW
	PUSHJ	P,SETIOD
	JRST	STOIOS

;THE FOLLOWING CODE APPEARS IN CLKSER:
;
;	AOS	TAC,UPTIME
;	CAML	TAC,CARALR	;IS IT TIME TO CHANGE THE CART'S STATE?
;	JRST	CARNXT
;CARDON:
;

↑CARNXT:SKIPE	TAC,CARBPT	;KICK CART. WAS PREVIOUS ACTION FINAL ONE?
	JRST	CARNXX		;IF NOT FINAL, ACT NATURAL
	MOVSI	TAC,CARCLK	;ELSE, CAUSE BUFFER CLEAR AND POSSIBLE
	SYSPIFF
	IDPB	TAC,CLKQ	;USE DPYSER CLOCK QUEUE
	SYSPIN
	MOVEI	TAC,CARDEF	;GET ADDRESS OF DEFAULT SETTING AND INTERVAL
	MOVEM	TAC,CARBPT	;AND POINT THE POINTER TO IT
CARNXX:	MOVEM AC1,CARAC1	;SAVE AN AC
	MOVE	AC1,(TAC)	;GET NEXT CONTROL WORD FROM BUFFER
	CONO	CAR,200000(AC1) ;DO THE ACTION
	XORI	AC1,177000	;LOW ORDER 9 BITS WILL BE 0
	CONO	CAR,600000(AC1)
	HLRZ	AC1,AC1		;GET THE TIME IN THE RIGHT HALF
	SUBI	AC1,=30		;COMPARE TO A HALF SEC
	JUMPLE	AC1,CARNXY
	HRLM	AC1,(TAC)	;IF GREATER, PUT REMAINDER BACK
	SETZB	TAC,AC1		;AND
	TROA	AC1,=30		;SET TIME TO A HALF SEC
CARNXY:	HLRZ	AC1,(TAC)	;RESTORE CORRECT TIME, IF less than 1/2 SEC
	ADD	AC1,UPTIME	;FIND WHEN NEXT TO KICK THIS CODE
	MOVEM	AC1,CARALR	;AND MAKE A NOTE OF IT
	JUMPGE	TAC,CARNXZ	;NOW, SEE IF WE SHOULD BOP THE POINTER
	AOBJN	TAC,.+2		;IF SO, BOP IT
	MOVEI	TAC,0		;AND IF ITS FINISHED, MAKE A MARK
	MOVEM	TAC,CARBPT	;AND SAVE IT
CARNXZ:	MOVE	AC1,CARAC1	;RESTORE AC1.
	JRST	CARDON

>;IFN CARNUM
	BEND	CARSER
